// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    field Array s;
    field int len, maxLen;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        if (maxLength < 0) {
            do Sys.error(14);
        }

        if (maxLength > 0) {
            let s = Array.new(maxLength);
        }

        let len = 0;
        let maxLen = maxLength;
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        if (maxLen > 0) {
            do s.dispose();
        }
        do Memory.deAlloc(this);
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return len;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        if (j > len) {
            do Sys.error(15);
        }
        return s[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        if (j > len) {
            do Sys.error(16);
        }
        let s[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if (len = maxLen) {
            do Sys.error(17);
        }
        let s[len] = c;
        let len = len + 1;
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (len = 0) {
            do Sys.error(18);
        }
        let len = len - 1;
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        return string2Int();
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        var int tmp, i;
        let i = 0;
        let tmp = 1;
        while (i < maxLen) {
            let tmp = tmp * 10;
            let i = i + 1;
        }
        if (val > (tmp-1)) {
            do Sys.error(19);
        }

        let len = 0;
        if (val < 0) {
            do appendChar(45); // 45: -
        }
        do int2String(Math.abs(val));
        return;
    }

    method void int2String(int n) {
        var int c, lastDigit;
        var string s, tmp;

        let lastDigit = n;
        while (lastDigit > 9) {
            let lastDigit = lastDigit - 10;
        }

        let c = lastDigit + 48;
        if (n < 10) {
            do appendChar(c);
            return;
        }
        do int2String(n/10);
        do appendChar(c);
        return;
    }

    method int string2Int() {
        var int i, v, d;
        var boolean isNeg;
        let i = 0;
        let v = 0;

        let isNeg = s[0] = 45; // 45: -
        if (isNeg) {
            let i = 1;
        }

        while (i < maxLen) {
            let d = s[i] - 48;
            let v = v*10 + d;
            let i = i + 1;
        }

        if (isNeg) {
            let v = -1 * v;
        }

        return v;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
