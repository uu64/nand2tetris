class Snake {
    field int nodeSize;
    // 0: up, 1: down, 2: left, 3: right
    field int direction;

    field SnakeNode head, tail;

    constructor Snake new(int headX, int headY, int initLen) {
        var int i;

        let nodeSize = 12;
        let direction = 3;

        let head = SnakeNode.new(-1, -1, null, null, nodeSize);
        let tail = SnakeNode.new(-1, -1, null, null, nodeSize);
        do head.setNext(tail);
        do tail.setPrev(head);

        let i = 0;
        while (i < initLen) {
            do addHead(headX, headY);
            let headX = headX + nodeSize;
            let i = i + 1;
        }

        return this;
    }

    method void addHead(int xVal, int yVal) {
        var SnakeNode added, first;

        let first = head.getNext();
        let added = SnakeNode.new(xVal, yVal, head, first, nodeSize);

        do head.setNext(added);
        do first.setPrev(added);

        do added.show();

        return;
    }

    method void removeTail() {
        var SnakeNode removed, last;

        let removed = tail.getPrev();
        let last = removed.getPrev();

        do tail.setPrev(last);
        do last.setNext(tail);

        do removed.hide();
        do removed.dispose();

        return;
    }

    method void dispose() {
        var SnakeNode n;

        let n = head;
        while (~(n = null)) {
            do n.dispose();
            let n = n.getNext();
        }

        do Memory.deAlloc(this);
        return;
    }

    method SnakeNode getHead() {
        return head.getNext();
    }

    method SnakeNode getTail() {
        return tail.getPrev();
    }

    method int getDirection() {
        return direction;
    }

    method void setDirection(int d) {
        if ((direction = 0) | (direction = 1)) {
            if ((d = 0) | (d = 1)) {
                // do nothing
                return;
            }
        }

        if ((direction = 2) | (direction = 3)) {
            if ((d = 2) | (d = 3)) {
                // do nothing
                return;
            }
        }

        let direction = d;
        return;
    }

    method void draw() {
        var SnakeNode node;
        let node = head.getNext();
        while (~(node.getNext() = null)) {
            do node.show();
            let node = node.getNext();
        }
        return;
    }

    method void move() {
        var SnakeNode n;
        var int headX, headY;

        let n = head.getNext();
        let headX = n.getX();
        let headY = n.getY();

        // up
        if (direction = 0) {
            do addHead(headX, headY - nodeSize);
        }
        // down
        if (direction = 1) {
            do addHead(headX, headY + nodeSize);
        }
        // left
        if (direction = 2) {
            do addHead(headX - nodeSize, headY);
        }
        // right
        if (direction = 3) {
            do addHead(headX + nodeSize, headY);
        }

        do removeTail();

        return;
    }
}